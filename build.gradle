plugins {
	id 'dev.architectury.loom' version '1.7-SNAPSHOT' apply false
	id 'architectury-plugin' version '3.4-SNAPSHOT'
	id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
	id 'java'
}

architectury {
	minecraft = minecraft_version
}

version = "$mod_version+$minecraft_version"

allprojects {
	group = maven_group
}

subprojects {
	apply plugin: 'dev.architectury.loom'
	apply plugin: 'architectury-plugin'

	version = "$mod_version+$project.name.$minecraft_version"

	base {
		archivesName = "$archives_name"
	}

	dependencies {
		minecraft "net.minecraft:minecraft:$minecraft_version"
		mappings loom.layered {
			it.mappings("net.fabricmc:yarn:$yarn_mappings:v2")
			it.mappings("dev.architectury:yarn-mappings-patch-neoforge:$yarn_mappings_patch_neoforge_version")
		}
	}

	java {
		withSourcesJar()

		sourceCompatibility = JavaVersion.toVersion(java_version)
		targetCompatibility = JavaVersion.toVersion(java_version)
	}

	ext {
		replaceProperties  = [
				'id': mod_id,
				'name': mod_name,
				'version': project.version,
				'license': mod_license,
				'authors': mod_authors,
				'description': mod_description,
				'icon': mod_icon_path,
				'minecraft_version': minecraft_version
		]
	}

	tasks.withType(JavaCompile).configureEach {
		it.options.release = java_version as int
	}
}
